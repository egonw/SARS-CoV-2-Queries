#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ endpoint_in_url: False
#+ enumerate:
#+   - lang:
#+     - en,en
#+     - nl,en
#+     - ja,en
#+     - es,en
#+     - pt,en

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>

SELECT DISTINCT ?variant ?variantLabel ?pango WHERE {
  VALUES ?variantType { wd:Q15304597 wd:Q75913269 }
  { ?variant p:P31 [ ps:P31 ?variantType ; pq:P642 wd:Q82069695 ] . }
  UNION
  { ?variant wdt:P31 wd:Q104450895 }
  OPTIONAL { ?variant wdt:P9632 ?pango }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,en". }
}

